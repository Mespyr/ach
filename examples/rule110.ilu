# rule110
# borrowed from https://gitlab.com/tsoding/porth/-/blob/master/examples/rule110.porth

@include "std/io.ilu"

def BOARD_SIZE 100 end

def rule110
    mem BOARD_SIZE 2 - +  # mem[BOARD_SIZE - 2]
    1 writeb              # mem[BOARD_SIZE - 2] = 1

    0 while dup BOARD_SIZE 2 - < do
        # write line into mem
        0 while dup BOARD_SIZE < do
            # check if mem[idx] is 0 or 1
            dup mem + readb if
                # write '*' if mem[i] == 1
                dup mem + BOARD_SIZE +  # mem[i + BOARD_SIZE]
                42 writeb               # mem[i + BOARD_SIZE] = 42
            else
                # write ' '  if mem[i] == 0
                dup mem + BOARD_SIZE +  # mem[i + BOARD_SIZE]
                32 writeb               # mem[i + BOARD_SIZE] = 32
            end

            1 +
        end

        pop
        
        # print line
        mem BOARD_SIZE +  # mem[BOARD_SIZE -->]
        BOARD_SIZE        # size of line
        println
        
        # pattern
        mem readb 1 <<
        mem 1 + readb
        orb

        1 while dup BOARD_SIZE 1 - < do
            # new pattern
            swp 1 << 7 andb over
            mem + 1 + readb orb

            over over 110 swp >> 1 andb
            swp mem + swp writeb
            swp

            1 +
        end
        
        pop pop

        1 +
    end
end

def main
    rule110
end
