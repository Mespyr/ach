# rule110
# borrowed and modified (slightly) from https://gitlab.com/tsoding/porth/-/blob/master/examples/rule110.porth

@include "std/core.ilu"

const BOARD_SIZE 100 end
memory row BOARD_SIZE end
memory display BOARD_SIZE 1 + end

def rule110 in
	row BOARD_SIZE 2 - +
	1 !8

	0 while dup BOARD_SIZE 2 - < do
		0 while dup BOARD_SIZE < do
			dup row + @8 if
				dup display + 46 !8
			else
				dup display + 32 !8
			end
			1 +
		end pop

		BOARD_SIZE display puts
		"\n" puts

		# pattern
		row @8 1 <<
		row 1 + @8
		or

		1 while dup BOARD_SIZE 1 - < do
			# new pattern
			swp 1 << 7 and over
			row + 1 + @8 or

			over over 110 swp >> 1 and
			swp row + swp !8
			swp

			1 +
		end pop pop
		1 +
	end pop
end

def main in
	rule110
end
